set(FFX_RUNTIME_SDK_COMMIT "60f4ea81909200d8542eca14dccb2628b763a9a3")
set(FFX_RUNTIME_BASE_URL
    "https://raw.githubusercontent.com/GPUOpen-LibrariesAndSDKs/FidelityFX-SDK/${FFX_RUNTIME_SDK_COMMIT}/Kits/FidelityFX/signedbin"
)
set(FFX_RUNTIME_FEATURE_ROOT "${CMAKE_CURRENT_BINARY_DIR}/ffx-runtime")
set(FFX_RUNTIME_SHADER_ROOT "${FFX_RUNTIME_FEATURE_ROOT}/Shaders")
set(FFX_RUNTIME_RELATIVE_DIRECTORY "Shaders/Upscaling/FidelityFX")
set(FFX_RUNTIME_DIRECTORY
    "${FFX_RUNTIME_FEATURE_ROOT}/${FFX_RUNTIME_RELATIVE_DIRECTORY}"
)
file(MAKE_DIRECTORY "${FFX_RUNTIME_DIRECTORY}")

function(download_ffx_runtime _filename _sha256)
    set(_destination "${FFX_RUNTIME_DIRECTORY}/${_filename}")
    file(
        DOWNLOAD "${FFX_RUNTIME_BASE_URL}/${_filename}"
        "${_destination}"
        EXPECTED_HASH "SHA256=${_sha256}"
        STATUS _download_status
        TLS_VERIFY ON
        TIMEOUT 120
        INACTIVITY_TIMEOUT 20
    )
    list(GET _download_status 0 _status_code)
    list(GET _download_status 1 _status_message)
    if(NOT _status_code EQUAL 0)
        file(REMOVE "${_destination}")
        message(
            FATAL_ERROR
            "Failed to download ${_filename}: ${_status_message}"
        )
    endif()
    set(FFX_RUNTIME_FILES ${FFX_RUNTIME_FILES} "${_destination}" PARENT_SCOPE)
endfunction()

set(FFX_RUNTIME_FILES "")
set(FFX_FRAMEGENERATION_DLL_OVERRIDE "" CACHE FILEPATH
    "Optional custom amd_fidelityfx_framegeneration_dx12.dll (for native FG experiments)")
if(FFX_FRAMEGENERATION_DLL_OVERRIDE)
    if(NOT EXISTS "${FFX_FRAMEGENERATION_DLL_OVERRIDE}")
        message(FATAL_ERROR "FFX_FRAMEGENERATION_DLL_OVERRIDE does not exist: ${FFX_FRAMEGENERATION_DLL_OVERRIDE}")
    endif()
    set(_ffx_custom_fg "${FFX_RUNTIME_DIRECTORY}/amd_fidelityfx_framegeneration_dx12.dll")
    configure_file("${FFX_FRAMEGENERATION_DLL_OVERRIDE}" "${_ffx_custom_fg}" COPYONLY)
    list(APPEND FFX_RUNTIME_FILES "${_ffx_custom_fg}")
    message(STATUS "Using custom FidelityFX frame-generation provider: ${FFX_FRAMEGENERATION_DLL_OVERRIDE}")
else()
    download_ffx_runtime(
        amd_fidelityfx_framegeneration_dx12.dll
        02297BEEDD285E822D3A64F314CF00FAF378DCEC0EDC47FF0C4DD71B3A8C2F18
    )
endif()
download_ffx_runtime(
    amd_fidelityfx_loader_dx12.dll
    E2D85AA05A9BD9ED8B38935FDF5199372CCA6F74C12015143BB6F945EE1608AA
)
download_ffx_runtime(
    amd_fidelityfx_denoiser_dx12.dll
    48F1E5888BA6A0A3D59A98B9751E37C392B0F7B8C223D0082D5C1F40642879D3
)
download_ffx_runtime(
    amd_fidelityfx_upscaler_dx12.dll
    D0DCCCC74A43C44BA435B7A369B456E0970D8A4464E4BD683119B374F2C9FB46
)

# FidelityFX Radiance Cache 0.9 provider from the pinned SDK release.
download_ffx_runtime(
    amd_fidelityfx_radiancecache_dx12.dll
    256DB18D924C8CD38923D04E3ECD210695D3F0F796B240EAB9663AD4D54E31A0
)

register_feature_payload(
    Upscaling
    FILES ${FFX_RUNTIME_FILES}
    DESTINATION "${FFX_RUNTIME_RELATIVE_DIRECTORY}"
)
